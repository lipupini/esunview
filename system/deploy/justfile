set dotenv-filename := "../config/.env"

# Confirm before running deployment
[confirm('Run the deployment?')]
confirm:
	cd ../.. && just test for-deploy
	just now

# This list of deployment commands
help:
	just --list

# Run a command from the `bin` directory on deployment server
bin command *args="":
	ssh ${SSH_USER}@${SSH_HOST} "cd ${DEPLOY_DIRECTORY} && bin/{{command}}.php {{args}}"

# Generate media cache files on deployment server
process-media:
	just bin media/process

# Generate RSA keys on deployment server
generate-keys collection="":
	just bin collection/generate-keys {{collection}}

# Push committed changes to remote `origin` https://github.com/lipupini/esunview.git
push:
	GIT_SSH_COMMAND="ssh -i ${GIT_SSH_KEY}" git push origin HEAD

# Apply to deployment remote `origin` https://github.com/lipupini/esunview.git
[confirm]
upgrade:
	ssh $SSH_USER@$SSH_HOST "cd ${DEPLOY_DIRECTORY} && git reset --hard origin/master && git fetch origin master && git checkout origin/master"

# Run the deployment!
now:
	just push && just upgrade
