set dotenv-filename := "../config/.env"

# Run the deployment!
default:
	cd ../.. && just test for-deploy
	just push && just upgrade

# Run a command from the `bin` directory on deployment server
bin command *args="":
	ssh $SSH_USER@$SSH_HOST "cd ${DEPLOY_DIRECTORY} && bin/{{command}}.php {{args}}"

# Generate media cache files on deployment server
process-media:
	just bin process-media

# Generate RSA keys on deployment server
generate-keys collection="":
	just bin generate-keys {{collection}}

# Push committed changes to remote `origin` https://github.com/lipupini/esunview.git
push:
	GIT_SSH_COMMAND="ssh -i ${GIT_SSH_KEY}" git push origin HEAD

# Apply remote `origin` https://github.com/lipupini/esunview.git to deployment
[confirm]
upgrade:
	ssh $SSH_USER@$SSH_HOST "cd ${DEPLOY_DIRECTORY} && git reset --hard origin/master && git fetch origin master && git checkout origin/master"
